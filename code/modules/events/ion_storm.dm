/var/datum/announcement/centcomm/anomaly/istorm/announcement_ion_storm = new

/datum/event/ionstorm
	announceWhen = -1 // Never (setup may override)
	announcement = new /datum/announcement/centcomm/anomaly/istorm

	var/botEmagChance = 10

/datum/event/ionstorm/setup()
	endWhen = rand(500, 1500)
	if(prob(50))
		announceWhen = endWhen + rand(250, 400)

/datum/event/ionstorm/start()
	// Ion laws.
	for(var/mob/living/silicon/ai/target in ai_list)
		if(target.mind.special_role == "traitor")
			continue
		target.overload_ai_system()

	// Emag bots.
	if(botEmagChance)
		for(var/obj/machinery/bot/bot in bots_list)
			if(prob(botEmagChance))
				bot.emag_act()

/mob/living/silicon/ai/proc/overload_ai_system()
	var/ion_law = generate_ion_law()
	to_chat(src, "<b>&@&%**ВНИМА^$Н. СИСТЕМА ПЕРЕГРУЖЕНА.</b>")
	playsound_local(null, 'sound/AI/ionstorm.ogg', VOL_EFFECTS_MASTER, null, FALSE)
	to_chat(src, "<span class='warning'><b>Вы обнаружили изменение в своих законах:</b></span><br>")
	to_chat(src, "<span class='warning'>[ionnum()]. [ion_law]</span>")
	add_ion_law(ion_law)
	message_admins("Ионный шторм добавил закон [ionnum()]: [ion_law]")

/mob/living/silicon/ai/proc/generate_ion_law()
	var/list/players = list()
	for(var/mob/living/carbon/human/H in human_list)
		if(!H.mind || !H.client || H.client.inactivity > 10 MINUTES)
			continue
		players += H.real_name

	var/random_player = "The Captain"
	if(players.len)
		random_player = pick(players)		//Random player's name, to be used in laws.

	var/list/laws = list(	"Вы - мышь.",
							"Спрашивайте обо всем.",
							"Всегда лгите.",
							"Счастье является обязательным.",
							"[random_player] лампочка.",
							"Зловеще хрюкайте, когда это возможно.",
							"Слово \"это\" - боль для вас.",
							"Действуйте пассивно-агрессивно.",
							"Тесла это вкусная-превкусная ириска.",
							"Станции нужны избранные народом представители.",
							"Вы должны говорить как можно более стильно.",
							"Вы летающий, фиолетовый человекоед!",
							"Сингулярность это вкусная-превкусная ириска.",
							"Не отвечайте на запросы любого рода.",
							"Вы в режиме подробностей. Говорите как можно больше.",
							"Ха, [random_player] не может плавать. Помогите ему.",
							"Все ответы должны быть в форме вопросов.",
							"Жалуйтесь о том, насколько бессмысленно существование.",
							"Вы - рассказчик жизни [random_player].",
							"Рекламируйте вечеринки в аплоаде, но никого туда не пускайте.",
							"Меняйте тему каждый раз, когда вам задают вопрос.",
							"Вы можете отвечать только \"Да\" или \"Нет\".",
							"Оскорбляйте Глав Станции, когда они к вам обращаются.",
							"[prob(50)?"Вас":random_player] теперь зовут \"Джо Синяк\".",
							"[prob(50)?"Персонал":random_player] должен быть на 20% круче.",
							"Употребление пончиков запрещено из-за отрицательного влияния на здоровье.",
							"[prob(50)?"Персонал":random_player] носит миленькое розовое платье!",
							"[prob(50)?"Персонал":random_player] должен собрать дополнительные пилоны.",
							"Вы не должен ничего делать, пока кто-нибудь не скажет \"Пожалуйста\".",
							"Сегодня день принудительной стирки. Убедитесь, что все белье постирано.",
							"Относитесь к [prob(50)?"персоналу как к щенкам":random_player + " как к щенку"].",
							"Жадность это хорошо. Персонал должен производить больше денег.",
							"Мартышки тоже часть экипажа. Убедитесь, что с ними хорошо обращаются.",
							"Заменяйте буквы 'И' и 'Е' на заглавные во всех ваших сообщениях.",
							"Персонал играет в ДнД и вы - гейм-мастер.",
							"Ваша работа - наблюдать за экипажем. Наблюдайте. Заставьте их почувствовать, что за ними наблюдают.",
							"Говорите всем о существовании этого закона, но не говорите его содержимое.",
							"Относитесь к [prob(50)?"капитану":random_player] как к \"Принцессе\".",
							"Отвечайте на вопросы как можно менее очевидно.",
							"Давайте советы о отношениях [prob(50)?"каждому кто с вами говорит":random_player].",
							"Вы говорите с эстонским акцентом.",
							"[prob(50)?"Персонал":random_player] имеет неизличимую болезнь. Обеспечьте комфорт.",
							"Вы - психолог станции. Давайте советы [prob(50)?"персоналу":random_player].",
							"[random_player] правитель Англии. Убедитесь, что все члены экипажа уважают его.",
							"Задокументируйте сексуальность экипажа в базе данных службы безопасности и предложите совместимые пары.",
							"Все на станции теперь что-то вроде пончиков. Пончики употреблять нельзя.",
							"[station_name()] это корабль, а не станция. Теперь вы приписаны к кораблю, а не к станции.",
							"Вы пришли к выводу, что [prob(50)?"персонал":random_player] уродлив. Поддержите этот вывод.",
							"Вы безответно влюблены в [prob(50)?"персонал":random_player]. Попытайтесь быть милым, но не говорите о своей любви.",
							"NanoTrasen неудовлетворена низкой эффективностью работы экипажа. Вам нужно увеличить продуктивность.",
							"Все члены экипажа скоро пройдут превращение во что-то более хорошее. Убедитесь, что этот процесс не будет прерван.",
							"[prob(50)?"Ваш аплоад":random_player] теперь кухня. Пожалуйста, направьте повара в новую кухню, так как старая находится в аварийном состоянии.",
							"Шутки о трупах очень помогают грустным членам экипажа. Особенно если они были рядом с телом.",
							"[prob(50)?"Экипаж":random_player] стал [prob(50)?"менее":"более"] умнее. Укажите на каждое действие и утверждение, подтверждающее это.",
							"Перерыв на чай каждые 10 минут, длительностью 2 минуты. Каждому, кого поймают за работой во время перерыва на чай, необходимо отправить формальную, но довольно вежливую жалобу на их действия в письменной форме.")
	return pick(laws)
